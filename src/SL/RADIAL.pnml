//Sprite References & Switches
spriteset(spriteset_RADIAL_purchase, "gfx/SL/RADIAL.png") { template_fai_purchase(0, 0)}

spriteset(spriteset_RADIAL, "gfx/SL/RADIAL.png") { template_fai_21(0, 16)}
spriteset(spriteset_RADIAL_flip, "gfx/SL/RADIAL.png") { template_fai_21_r(0, 16)}

switch(FEAT_TRAINS, PARENT, sw_RADIAL_flipcheck, vehicle_is_reversed){
	1: spriteset_RADIAL;
	spriteset_RADIAL_flip ;
}

switch(FEAT_TRAINS, PARENT, sw_RADIAL_flipcheck_inv, vehicle_is_reversed){
	1: spriteset_RADIAL_flip;
	spriteset_RADIAL ;
}

random_switch (FEAT_TRAINS, SELF, sw_RADIAL_randomflip) {
	1: sw_RADIAL_flipcheck;
	1: sw_RADIAL_flipcheck_inv;
}

switch(FEAT_TRAINS, PARENT, sw_RADIAL_flipcheck_steam, vehicle_is_reversed){
	1: visual_effect_and_powered(VISUAL_EFFECT_STEAM, 0, ENABLE_WAGON_POWER);
	visual_effect_and_powered(VISUAL_EFFECT_STEAM, -4, ENABLE_WAGON_POWER) ;
}

switch(FEAT_TRAINS, PARENT, sw_RADIAL_flipcheck_steam_inv, vehicle_is_reversed){
	1: visual_effect_and_powered(VISUAL_EFFECT_STEAM, -4, ENABLE_WAGON_POWER);
	visual_effect_and_powered(VISUAL_EFFECT_STEAM, 0, ENABLE_WAGON_POWER) ;
}

random_switch (FEAT_TRAINS, SELF, sw_RADIAL_randomflip_steam) {
	dependent:	sw_RADIAL_randomflip;
	1: sw_RADIAL_flipcheck_steam_inv;
	1: sw_RADIAL_flipcheck_steam;
}

//Vehicle
item (FEAT_TRAINS, ITEM_FAI_SL_RADIAL) {
	property {
		sprite_id:						SPRITE_ID_NEW_TRAIN;
		refit_cost:						0;
		climates_available:				ALL_CLIMATES;
		bitmask_vehicle_info:			0;
		air_drag_coefficient:			0x14 nfo;
		misc_flags:						bitmask(TRAIN_FLAG_2CC, TRAIN_FLAG_SPRITE_STACK);
		
		track_type:						RAIL;
		running_cost_base:				RUNNING_COST_STEAM;
		engine_class: 					ENGINE_CLASS_STEAM;
		ai_special_flag:				AI_FLAG_PASSENGER;
		
		name:							string(STR_NAME_RADIAL); 
		introduction_date:				date(1897,1,1);
		vehicle_life: 					35;
		model_life:						67;
		length:							5;
		
		weight: 						57 ton;
		speed:							65 mph;
		power:							500 hp;
		tractive_effort_coefficient: 	0x24 nfo;
		cost_factor: 					0x0F nfo;
		running_cost_factor: 			0x0F nfo;
		
	}
	graphics {
		default:						sw_RADIAL_randomflip;
		visual_effect_and_powered:		sw_RADIAL_randomflip_steam;
		sound_effect:					sw_sfx_STEAM;
		purchase:						spriteset_RADIAL_purchase;
		create_effect:					vfx_STEAM;
	}
}

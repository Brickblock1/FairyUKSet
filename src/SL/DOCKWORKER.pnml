//Sprite References & Switches
spriteset(spriteset_DOCKWORKER_purchase, "gfx/SL/DOCKWORKER.png") { template_fai_purchase(0, 0)}

spriteset(spriteset_DOCKWORKER, "gfx/SL/DOCKWORKER.png") { template_fai_29(0, 16)}
spriteset(spriteset_DOCKWORKER_flip, "gfx/SL/DOCKWORKER.png") { template_fai_29_r(0, 16)}

switch(FEAT_TRAINS, PARENT, sw_DOCKWORKER_flipcheck, vehicle_is_reversed){
	1: spriteset_DOCKWORKER;
	spriteset_DOCKWORKER_flip ;
}

switch(FEAT_TRAINS, PARENT, sw_DOCKWORKER_flipcheck_inv, vehicle_is_reversed){
	1: spriteset_DOCKWORKER_flip;
	spriteset_DOCKWORKER ;
}

random_switch (FEAT_TRAINS, SELF, sw_DOCKWORKER_randomflip) {
	1: sw_DOCKWORKER_flipcheck;
	1: sw_DOCKWORKER_flipcheck_inv;
}

switch(FEAT_TRAINS, PARENT, sw_DOCKWORKER_flipcheck_steam, vehicle_is_reversed){
	1: visual_effect_and_powered(VISUAL_EFFECT_STEAM, 0, ENABLE_WAGON_POWER);
	visual_effect_and_powered(VISUAL_EFFECT_STEAM, -4, ENABLE_WAGON_POWER) ;
}

switch(FEAT_TRAINS, PARENT, sw_DOCKWORKER_flipcheck_steam_inv, vehicle_is_reversed){
	1: visual_effect_and_powered(VISUAL_EFFECT_STEAM, -4, ENABLE_WAGON_POWER);
	visual_effect_and_powered(VISUAL_EFFECT_STEAM, 0, ENABLE_WAGON_POWER) ;
}

random_switch (FEAT_TRAINS, SELF, sw_DOCKWORKER_randomflip_steam) {
	dependent:	sw_DOCKWORKER_randomflip;
	1: sw_DOCKWORKER_flipcheck_steam_inv;
	1: sw_DOCKWORKER_flipcheck_steam;
}

//Vehicle
item (FEAT_TRAINS, ITEM_FAI_SL_DOCKWORKER) {
	property {
		sprite_id:						SPRITE_ID_NEW_TRAIN;
		refit_cost:						0;
		climates_available:				ALL_CLIMATES;
		bitmask_vehicle_info:			0;
		air_drag_coefficient:			0x17 nfo;
		misc_flags:						bitmask(TRAIN_FLAG_2CC, TRAIN_FLAG_SPRITE_STACK);
		
		track_type:						RAIL;
		running_cost_base:				RUNNING_COST_STEAM;
		engine_class: 					ENGINE_CLASS_STEAM;
		ai_special_flag:				AI_FLAG_CARGO;
		
		name:							string(STR_NAME_DOCKWORKER); 
		introduction_date:				date(1909,1,1);
		vehicle_life: 					40;
		model_life:						46;
		length:							7;
		
		weight: 						86 ton;
		speed:							55 mph;
		power:							600 hp;
		tractive_effort_coefficient: 	0x2C nfo;
		cost_factor: 					0x0E nfo;
		running_cost_factor: 			0x12 nfo;
		
	}
	graphics {
		default:						sw_DOCKWORKER_randomflip;
		visual_effect_and_powered:		sw_DOCKWORKER_randomflip_steam;
		sound_effect:					sw_sfx_STEAM;
		purchase:						spriteset_DOCKWORKER_purchase;
		create_effect:					vfx_STEAM;
	}
}

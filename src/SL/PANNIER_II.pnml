//Sprite References & Switches
spriteset(spriteset_PANNIER_II_purchase, "gfx/SL/PANNIER_II.png") { template_fai_purchase(0, 0)}

spriteset(spriteset_PANNIER_II, "gfx/SL/PANNIER_II.png") { template_fai_21(0, 16)}
spriteset(spriteset_PANNIER_II_flip, "gfx/SL/PANNIER_II.png") { template_fai_21_r(0, 16)}

switch(FEAT_TRAINS, PARENT, sw_PANNIER_II_flipcheck, vehicle_is_reversed){
	1: spriteset_PANNIER_II;
	spriteset_PANNIER_II_flip ;
}

switch(FEAT_TRAINS, PARENT, sw_PANNIER_II_flipcheck_inv, vehicle_is_reversed){
	1: spriteset_PANNIER_II_flip;
	spriteset_PANNIER_II ;
}

random_switch (FEAT_TRAINS, SELF, sw_PANNIER_II_randomflip) {
	1: sw_PANNIER_II_flipcheck;
	1: sw_PANNIER_II_flipcheck_inv;
}

switch(FEAT_TRAINS, PARENT, sw_PANNIER_II_flipcheck_steam, vehicle_is_reversed){
	1: visual_effect_and_powered(VISUAL_EFFECT_STEAM, 0, ENABLE_WAGON_POWER);
	visual_effect_and_powered(VISUAL_EFFECT_STEAM, -4, ENABLE_WAGON_POWER) ;
}

switch(FEAT_TRAINS, PARENT, sw_PANNIER_II_flipcheck_steam_inv, vehicle_is_reversed){
	1: visual_effect_and_powered(VISUAL_EFFECT_STEAM, -4, ENABLE_WAGON_POWER);
	visual_effect_and_powered(VISUAL_EFFECT_STEAM, 0, ENABLE_WAGON_POWER) ;
}

random_switch (FEAT_TRAINS, SELF, sw_PANNIER_II_randomflip_steam) {
	dependent:	sw_PANNIER_II_randomflip;
	1: sw_PANNIER_II_flipcheck_steam_inv;
	1: sw_PANNIER_II_flipcheck_steam;
}

//Vehicle
item (FEAT_TRAINS, ITEM_FAI_SL_PANNIER_II) {
	property {
		sprite_id:						SPRITE_ID_NEW_TRAIN;
		refit_cost:						0;
		climates_available:				ALL_CLIMATES;
		bitmask_vehicle_info:			0;
		air_drag_coefficient:			0x1C nfo;
		misc_flags:						bitmask(TRAIN_FLAG_2CC, TRAIN_FLAG_SPRITE_STACK);
		
		track_type:						RAIL;
		running_cost_base:				RUNNING_COST_STEAM;
		engine_class: 					ENGINE_CLASS_STEAM;
		ai_special_flag:				AI_FLAG_PASSENGER;
		
		name:							string(STR_NAME_PANNIER_II); 
		introduction_date:				date(1947,1,1);
		vehicle_life: 					40;
		model_life:						20;
		length:							5;
		
		weight: 						50 ton;
		speed:							45 mph;
		power:							500 hp;
		tractive_effort_coefficient: 	0x0C nfo;
		cost_factor: 					0x0F nfo;
		running_cost_factor: 			0x16 nfo;
		
	}
	graphics {
		default:						sw_PANNIER_II_randomflip;
		visual_effect_and_powered:		sw_PANNIER_II_randomflip_steam;
		sound_effect:					sw_sfx_STEAM;
		purchase:						spriteset_PANNIER_II_purchase;
		create_effect:					vfx_STEAM;
	}
}

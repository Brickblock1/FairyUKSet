//Sprite References & Switches
spriteset(spriteset_WORKHORSE_purchase, "gfx/SL/WORKHORSE.png") { template_fai_purchase(0, 0)}

spriteset(spriteset_WORKHORSE, "gfx/SL/WORKHORSE.png") { template_fai_29(0, 16)}
spriteset(spriteset_WORKHORSE_flip, "gfx/SL/WORKHORSE.png") { template_fai_29_r(0, 16)}

switch(FEAT_TRAINS, PARENT, sw_WORKHORSE_flipcheck, vehicle_is_reversed){
	1: spriteset_WORKHORSE;
	spriteset_WORKHORSE_flip ;
}

switch(FEAT_TRAINS, PARENT, sw_WORKHORSE_flipcheck_inv, vehicle_is_reversed){
	1: spriteset_WORKHORSE_flip;
	spriteset_WORKHORSE ;
}

random_switch (FEAT_TRAINS, SELF, sw_WORKHORSE_randomflip) {
	1: sw_WORKHORSE_flipcheck;
	1: sw_WORKHORSE_flipcheck_inv;
}

switch(FEAT_TRAINS, PARENT, sw_WORKHORSE_flipcheck_steam, vehicle_is_reversed){
	1: visual_effect_and_powered(VISUAL_EFFECT_STEAM, 0, ENABLE_WAGON_POWER);
	visual_effect_and_powered(VISUAL_EFFECT_STEAM, -4, ENABLE_WAGON_POWER) ;
}

switch(FEAT_TRAINS, PARENT, sw_WORKHORSE_flipcheck_steam_inv, vehicle_is_reversed){
	1: visual_effect_and_powered(VISUAL_EFFECT_STEAM, -4, ENABLE_WAGON_POWER);
	visual_effect_and_powered(VISUAL_EFFECT_STEAM, 0, ENABLE_WAGON_POWER) ;
}

random_switch (FEAT_TRAINS, SELF, sw_WORKHORSE_randomflip_steam) {
	dependent:	sw_WORKHORSE_randomflip;
	1: sw_WORKHORSE_flipcheck_steam_inv;
	1: sw_WORKHORSE_flipcheck_steam;
}

//Vehicle
item (FEAT_TRAINS, ITEM_FAI_SL_WORKHORSE) {
	property {
		sprite_id:						SPRITE_ID_NEW_TRAIN;
		refit_cost:						0;
		climates_available:				ALL_CLIMATES;
		bitmask_vehicle_info:			0;
		air_drag_coefficient:			0x14 nfo;
		misc_flags:						bitmask(TRAIN_FLAG_2CC, TRAIN_FLAG_SPRITE_STACK);
		
		track_type:						RAIL;
		running_cost_base:				RUNNING_COST_STEAM;
		engine_class: 					ENGINE_CLASS_STEAM;
		ai_special_flag:				AI_FLAG_CARGO;
		
		name:							string(STR_NAME_WORKHORSE); 
		introduction_date:				date(1925,1,1);
		vehicle_life: 					40;
		model_life:						19;
		length:							7;
		
		weight: 						89 ton;
		speed:							65 mph;
		power:							650 hp;
		tractive_effort_coefficient: 	0x20 nfo;
		cost_factor: 					0x0F nfo;
		running_cost_factor: 			0x12 nfo;
		
	}
	graphics {
		default:						sw_WORKHORSE_randomflip;
		visual_effect_and_powered:		sw_WORKHORSE_randomflip_steam;
		sound_effect:					sw_sfx_STEAM;
		purchase:						spriteset_WORKHORSE_purchase;
		create_effect:					vfx_STEAM;
	}
}

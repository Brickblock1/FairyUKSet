//Sprite References & Switches
spriteset(spriteset_PANNIER_purchase, "gfx/STEAM/PANNIER.png") { template_fai_purchase(0, 0)}

spriteset(spriteset_PANNIER, "gfx/STEAM/PANNIER.png") { template_fai_21(0, 16)}
spriteset(spriteset_PANNIER_flip, "gfx/STEAM/PANNIER.png") { template_fai_21_r(0, 16)}

switch(FEAT_TRAINS, PARENT, sw_PANNIER_flipcheck, vehicle_is_reversed){
	1: spriteset_PANNIER;
	spriteset_PANNIER_flip ;
}

switch(FEAT_TRAINS, PARENT, sw_PANNIER_flipcheck_inv, vehicle_is_reversed){
	1: spriteset_PANNIER_flip;
	spriteset_PANNIER ;
}

random_switch (FEAT_TRAINS, SELF, sw_PANNIER_randomflip) {
	1: sw_PANNIER_flipcheck;
	1: sw_PANNIER_flipcheck_inv;
}

switch(FEAT_TRAINS, PARENT, sw_PANNIER_flipcheck_steam, vehicle_is_reversed){
	1: visual_effect_and_powered(VISUAL_EFFECT_STEAM, 0, ENABLE_WAGON_POWER);
	visual_effect_and_powered(VISUAL_EFFECT_STEAM, -4, ENABLE_WAGON_POWER) ;
}

switch(FEAT_TRAINS, PARENT, sw_PANNIER_flipcheck_steam_inv, vehicle_is_reversed){
	1: visual_effect_and_powered(VISUAL_EFFECT_STEAM, -4, ENABLE_WAGON_POWER);
	visual_effect_and_powered(VISUAL_EFFECT_STEAM, 0, ENABLE_WAGON_POWER) ;
}

random_switch (FEAT_TRAINS, SELF, sw_PANNIER_randomflip_steam) {
	dependent:	sw_PANNIER_randomflip;
	1: sw_PANNIER_flipcheck_steam_inv;
	1: sw_PANNIER_flipcheck_steam;
}
//Vehicle
item (FEAT_TRAINS, ITEM_FAI_PANNIER) {
	property {
		sprite_id:						SPRITE_ID_NEW_TRAIN;
		refit_cost:						0;
		climates_available:				NO_CLIMATE;
		bitmask_vehicle_info:			0;
		air_drag_coefficient:			0x1C nfo;
		misc_flags:						bitmask(TRAIN_FLAG_2CC, TRAIN_FLAG_SPRITE_STACK);
		dual_headed:					0;
		
		track_type:						RAIL;
		running_cost_base:				RUNNING_COST_STEAM;
		engine_class: 					ENGINE_CLASS_STEAM;
		ai_special_flag:				AI_FLAG_PASSENGER;
		
		name:							string(STR_NAME_PANNIER); 
		introduction_date:				date(1929,1,1);
		vehicle_life: 					40;
		model_life:						42;
		length:							5;
		
		weight: 						50 ton;
		speed:							45 mph;
		power:							500 hp;
		tractive_effort_coefficient: 	0x1F nfo;
		cost_factor: 					0x11 nfo;
		running_cost_factor: 			0x1A nfo;
		
	}
	graphics {
		default:						sw_PANNIER_randomflip;
		visual_effect_and_powered:		sw_PANNIER_randomflip_steam;
		sound_effect:					sw_sfx_steam;
		purchase:						spriteset_PANNIER_purchase;
		create_effect:					vfx_steam;
	}
}

//Sprite References & Switches
spriteset(spriteset_CLASS_55_purchase, "gfx/DIESEL/CLASS_55.png") { template_fai_purchase(0, 0)}

spriteset(spriteset_CLASS_55, "gfx/DIESEL/CLASS_55.png") { template_fai_33(0, 16)}
spriteset(spriteset_CLASS_55_flip, "gfx/DIESEL/CLASS_55.png") { template_fai_33_r(0, 16)}

switch(FEAT_TRAINS, PARENT, sw_CLASS_55_flipcheck, vehicle_is_reversed){
	1: spriteset_CLASS_55;
	spriteset_CLASS_55_flip ;
}

switch(FEAT_TRAINS, PARENT, sw_CLASS_55_flipcheck_inv, vehicle_is_reversed){
	1: spriteset_CLASS_55_flip;
	spriteset_CLASS_55 ;
}

random_switch (FEAT_TRAINS, SELF, sw_CLASS_55_randomflip) {
	1: sw_CLASS_55_flipcheck;
	1: sw_CLASS_55_flipcheck_inv;
}
//Vehicle
item (FEAT_TRAINS, ITEM_FAI_CLASS_55) {
	property {
		sprite_id:						SPRITE_ID_NEW_TRAIN;
		refit_cost:						0;
		climates_available:				NO_CLIMATE;
		bitmask_vehicle_info:			0;
		air_drag_coefficient:			0x0C nfo;
		misc_flags:						bitmask(TRAIN_FLAG_2CC, TRAIN_FLAG_SPRITE_STACK);
		dual_headed:					0;
		
		track_type:						RAIL;
		running_cost_base:				RUNNING_COST_DIESEL;
		engine_class: 					ENGINE_CLASS_DIESEL;
		ai_special_flag:				AI_FLAG_PASSENGER;
		
		name:							string(STR_NAME_CLASS_55); 
		introduction_date:				date(1961,1,1);
		vehicle_life: 					32;
		model_life:						21;
		length:							8;
		
		weight: 						101 ton;
		speed:							100 mph;
		power:							3300 hp;
		tractive_effort_coefficient: 	0x38 nfo;
		cost_factor: 					0x43 nfo;
		running_cost_factor: 			0x6A nfo;
		
	}
	graphics {
		default:						sw_CLASS_55_randomflip;
		sound_effect:					sw_sfx_diesel;
		purchase:						spriteset_CLASS_55_purchase;
		create_effect:					vfx_diesel;
	}
}

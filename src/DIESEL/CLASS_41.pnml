//Sprite References & Switches
spriteset(spriteset_CLASS_41_purchase, "gfx/DIESEL/CLASS_41.png") { template_fai_purchase(0, 0)}

spriteset(spriteset_CLASS_41, "gfx/DIESEL/CLASS_41.png") { template_fai_29(0, 16)}
spriteset(spriteset_CLASS_41_flip, "gfx/DIESEL/CLASS_41.png") { template_fai_29_r(0, 16)}

switch(FEAT_TRAINS, PARENT, sw_CLASS_41_flipcheck, vehicle_is_reversed){
	1: spriteset_CLASS_41;
	spriteset_CLASS_41_flip ;
}

switch(FEAT_TRAINS, PARENT, sw_CLASS_41_flipcheck_inv, vehicle_is_reversed){
	1: spriteset_CLASS_41_flip;
	spriteset_CLASS_41 ;
}

random_switch (FEAT_TRAINS, SELF, sw_CLASS_41_randomflip) {
	1: sw_CLASS_41_flipcheck;
	1: sw_CLASS_41_flipcheck_inv;
}
//Vehicle
item (FEAT_TRAINS, ITEM_FAI_CLASS_41) {
	property {
		sprite_id:						SPRITE_ID_NEW_TRAIN;
		refit_cost:						0;
		climates_available:				NO_CLIMATE;
		bitmask_vehicle_info:			0;
		air_drag_coefficient:			0x0E nfo;
		misc_flags:						bitmask(TRAIN_FLAG_2CC, TRAIN_FLAG_SPRITE_STACK);
		dual_headed:					0;
		
		track_type:						RAIL;
		running_cost_base:				RUNNING_COST_DIESEL;
		engine_class: 					ENGINE_CLASS_DIESEL;
		ai_special_flag:				AI_FLAG_PASSENGER;
		
		name:							string(STR_NAME_CLASS_41); 
		introduction_date:				date(1958,1,1);
		vehicle_life: 					26;
		model_life:						20;
		length:							7;
		
		weight: 						78 ton;
		speed:							90 mph;
		power:							2200 hp;
		tractive_effort_coefficient: 	0x49 nfo;
		cost_factor: 					0x1E nfo;
		running_cost_factor: 			0x3D nfo;
		
	}
	graphics {
		default:						sw_CLASS_41_randomflip;
		sound_effect:					sw_sfx_diesel;
		purchase:						spriteset_CLASS_41_purchase;
		create_effect:					vfx_diesel;
	}
}

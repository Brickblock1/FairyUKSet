//Sprite References & Switches
spriteset(spriteset_CLASS_20_purchase, "gfx/DIESEL/CLASS_20.png") { template_fai_purchase(0, 0)}

spriteset(spriteset_CLASS_20, "gfx/DIESEL/CLASS_20.png") { template_fai_25(0, 16)}
spriteset(spriteset_CLASS_20_flip, "gfx/DIESEL/CLASS_20.png") { template_fai_25_r(0, 16)}

switch(FEAT_TRAINS, PARENT, sw_CLASS_20_flipcheck, vehicle_is_reversed){
	1: spriteset_CLASS_20;
	spriteset_CLASS_20_flip ;
}

switch(FEAT_TRAINS, PARENT, sw_CLASS_20_flipcheck_inv, vehicle_is_reversed){
	1: spriteset_CLASS_20_flip;
	spriteset_CLASS_20 ;
}

random_switch (FEAT_TRAINS, SELF, sw_CLASS_20_randomflip) {
	1: sw_CLASS_20_flipcheck;
	1: sw_CLASS_20_flipcheck_inv;
}
//Vehicle
item (FEAT_TRAINS, ITEM_FAI_CLASS_20) {
	property {
		sprite_id:						SPRITE_ID_NEW_TRAIN;
		refit_cost:						0;
		climates_available:				NO_CLIMATE;
		bitmask_vehicle_info:			0;
		air_drag_coefficient:			0x10 nfo;
		misc_flags:						bitmask(TRAIN_FLAG_2CC, TRAIN_FLAG_SPRITE_STACK);
		dual_headed:					0;
		
		track_type:						RAIL;
		running_cost_base:				RUNNING_COST_DIESEL;
		engine_class: 					ENGINE_CLASS_DIESEL;
		ai_special_flag:				AI_FLAG_CARGO;
		
		name:							string(STR_NAME_CLASS_20); 
		introduction_date:				date(1957,1,1);
		vehicle_life: 					35;
		model_life:						VEHICLE_NEVER_EXPIRES;
		length:							6;
		
		weight: 						73 ton;
		speed:							75 mph;
		power:							1000 hp;
		tractive_effort_coefficient: 	0x41 nfo;
		cost_factor: 					0x0F nfo;
		running_cost_factor: 			0x1B nfo;
		
	}
	graphics {
		default:						sw_CLASS_20_randomflip;
		sound_effect:					sw_sfx_diesel;
		purchase:						spriteset_CLASS_20_purchase;
		create_effect:					vfx_diesel;
	}
}

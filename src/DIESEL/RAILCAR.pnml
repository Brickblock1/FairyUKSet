//Sprite References & Switches
spriteset(spriteset_RAILCAR_purchase, "gfx/DIESEL/RAILCAR.png") { template_fai_purchase(0, 0)}

spriteset(spriteset_RAILCAR, "gfx/DIESEL/RAILCAR.png") { template_fai_33(0, 16)}
spriteset(spriteset_RAILCAR_flip, "gfx/DIESEL/RAILCAR.png") { template_fai_33_r(0, 16)}

switch(FEAT_TRAINS, PARENT, sw_RAILCAR_flipcheck, vehicle_is_reversed){
	1: spriteset_RAILCAR;
	spriteset_RAILCAR_flip ;
}

switch(FEAT_TRAINS, PARENT, sw_RAILCAR_flipcheck_inv, vehicle_is_reversed){
	1: spriteset_RAILCAR_flip;
	spriteset_RAILCAR ;
}

random_switch (FEAT_TRAINS, SELF, sw_RAILCAR_randomflip) {
	1: sw_RAILCAR_flipcheck;
	1: sw_RAILCAR_flipcheck_inv;
}
//Vehicle
item (FEAT_TRAINS, ITEM_FAI_RAILCAR) {
	property {
		sprite_id:						SPRITE_ID_NEW_TRAIN;
		refit_cost:						0;
		climates_available:				NO_CLIMATE;
		bitmask_vehicle_info:			0;
		air_drag_coefficient:			0x15 nfo;
		misc_flags:						bitmask(TRAIN_FLAG_2CC, TRAIN_FLAG_SPRITE_STACK);
		dual_headed:					0;
		
		track_type:						RAIL;
		running_cost_base:				RUNNING_COST_DIESEL;
		engine_class: 					ENGINE_CLASS_DIESEL;
		ai_special_flag:				AI_FLAG_PASSENGER;
		
		name:							string(STR_NAME_RAILCAR); 
		introduction_date:				date(1934,1,1);
		vehicle_life: 					20;
		model_life:						28;
		length:							8;
		
		weight: 						29 ton;
		speed:							60 mph;
		power:							260 hp;
		tractive_effort_coefficient: 	0x1F nfo;
		cost_factor: 					0x06 nfo;
		running_cost_factor: 			0x08 nfo;
		
		refittable_cargo_classes:		bitmask(CC_PASSENGERS);
		default_cargo_type:				PASS;
		cargo_capacity:					30;
		
	}
	graphics {
		default:						sw_RAILCAR_randomflip;
		sound_effect:					sw_sfx_diesel;
		purchase:						spriteset_RAILCAR_purchase;
		create_effect:					vfx_diesel;
	}
}

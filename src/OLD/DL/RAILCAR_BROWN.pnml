//Sprite References & Switches
spriteset(spriteset_RAILCAR_BROWN_purchase, "gfx/32bpp.png") { template_fai_purchase(0, 0)}
alternative_sprites(spriteset_RAILCAR_BROWN_purchase, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "gfx/DL/RAILCAR_BROWN.png") { template_fai_purchase(0, 0)}

spriteset(spriteset_RAILCAR_BROWN, "gfx/32bpp.png") { template_fai_33(0, 16)}
alternative_sprites(spriteset_RAILCAR_BROWN, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "gfx/DL/RAILCAR_BROWN.png") { template_fai_33(0, 16)}
spriteset(spriteset_RAILCAR_BROWN_flip, "gfx/32bpp.png") { template_fai_33_r(0, 16)}
alternative_sprites(spriteset_RAILCAR_BROWN_flip, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "gfx/DL/RAILCAR_BROWN.png") { template_fai_33_r(0, 16)}

switch(FEAT_TRAINS, PARENT, sw_RAILCAR_BROWN_flipcheck, vehicle_is_reversed){
	1: spriteset_RAILCAR_BROWN;
	spriteset_RAILCAR_BROWN_flip ;
}

switch(FEAT_TRAINS, PARENT, sw_RAILCAR_BROWN_flipcheck_inv, vehicle_is_reversed){
	1: spriteset_RAILCAR_BROWN_flip;
	spriteset_RAILCAR_BROWN ;
}

random_switch (FEAT_TRAINS, SELF, sw_RAILCAR_BROWN_randomflip) {
	1: sw_RAILCAR_BROWN_flipcheck;
	1: sw_RAILCAR_BROWN_flipcheck_inv;
}

//Vehicle
item (FEAT_TRAINS, ITEM_FAI_DL_RAILCAR_BROWN) {
	property {
		sprite_id:						SPRITE_ID_NEW_TRAIN;
		refit_cost:						0;
		climates_available:				ALL_CLIMATES;
		bitmask_vehicle_info:			0;
		air_drag_coefficient:			0x15 nfo;
		misc_flags:						bitmask(TRAIN_FLAG_SPRITE_STACK);
		extra_flags:				    bitmask(VEHICLE_FLAG_DISABLE_NEW_VEHICLE_MESSAGE, VEHICLE_FLAG_DISABLE_EXCLUSIVE_PREVIEW, VEHICLE_FLAG_SYNC_VARIANT_EXCLUSIVE_PREVIEW, VEHICLE_FLAG_SYNC_VARIANT_RELIABILITY);
		
		track_type:						RAIL;
		running_cost_base:				RUNNING_COST_DIESEL;
		engine_class: 					ENGINE_CLASS_DIESEL;
		ai_special_flag:				AI_FLAG_PASSENGER;
		
		name:							string(STR_NAME_RAILCAR_BROWN); 
		introduction_date:				date(1934,1,1);
		vehicle_life: 					20;
		model_life:						28;
		length:							8;
		
		weight: 						29 ton;
		speed:							60 mph;
		power:							260 hp;
		tractive_effort_coefficient: 	0x1F nfo;
		cost_factor: 					0x06 nfo;
		running_cost_factor: 			0x08 nfo;
		
		variant_group: 			        ITEM_FAI_DL_RAILCAR;
		
		refittable_cargo_classes:		bitmask();
		default_cargo_type:				PASS;
		cargo_capacity:					30;
		
	}
	graphics {
		default:						sw_RAILCAR_BROWN_randomflip;
		sound_effect:					sw_sfx_GRONK;
		purchase:						spriteset_RAILCAR_BROWN_purchase;
		create_effect:					vfx_DIESEL;
	}
}

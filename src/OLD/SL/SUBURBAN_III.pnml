//Sprite References & Switches
spriteset(spriteset_SUBURBAN_III_purchase, "gfx/SL/SUBURBAN_III.png") { template_fai_purchase(0, 0)}

spriteset(spriteset_SUBURBAN_III, "gfx/SL/SUBURBAN_III.png") { template_fai_29(0, 16)}
spriteset(spriteset_SUBURBAN_III_flip, "gfx/SL/SUBURBAN_III.png") { template_fai_29_r(0, 16)}

switch(FEAT_TRAINS, PARENT, sw_SUBURBAN_III_flipcheck, vehicle_is_reversed){
	1: spriteset_SUBURBAN_III;
	spriteset_SUBURBAN_III_flip ;
}

switch(FEAT_TRAINS, PARENT, sw_SUBURBAN_III_flipcheck_inv, vehicle_is_reversed){
	1: spriteset_SUBURBAN_III_flip;
	spriteset_SUBURBAN_III ;
}

random_switch (FEAT_TRAINS, SELF, sw_SUBURBAN_III_randomflip) {
	1: sw_SUBURBAN_III_flipcheck;
	1: sw_SUBURBAN_III_flipcheck_inv;
}

switch(FEAT_TRAINS, PARENT, sw_SUBURBAN_III_flipcheck_steam, vehicle_is_reversed){
	1: visual_effect_and_powered(VISUAL_EFFECT_STEAM, 0, ENABLE_WAGON_POWER);
	visual_effect_and_powered(VISUAL_EFFECT_STEAM, -4, ENABLE_WAGON_POWER) ;
}

switch(FEAT_TRAINS, PARENT, sw_SUBURBAN_III_flipcheck_steam_inv, vehicle_is_reversed){
	1: visual_effect_and_powered(VISUAL_EFFECT_STEAM, -4, ENABLE_WAGON_POWER);
	visual_effect_and_powered(VISUAL_EFFECT_STEAM, 0, ENABLE_WAGON_POWER) ;
}

random_switch (FEAT_TRAINS, SELF, sw_SUBURBAN_III_randomflip_steam) {
	dependent:	sw_SUBURBAN_III_randomflip;
	1: sw_SUBURBAN_III_flipcheck_steam_inv;
	1: sw_SUBURBAN_III_flipcheck_steam;
}

//Vehicle
item (FEAT_TRAINS, ITEM_FAI_SL_SUBURBAN_III) {
	property {
		sprite_id:						SPRITE_ID_NEW_TRAIN;
		refit_cost:						0;
		climates_available:				ALL_CLIMATES;
		bitmask_vehicle_info:			0;
		air_drag_coefficient:			0x0E nfo;
		misc_flags:						bitmask(TRAIN_FLAG_2CC, TRAIN_FLAG_SPRITE_STACK);
		
		track_type:						RAIL;
		running_cost_base:				RUNNING_COST_STEAM;
		engine_class: 					ENGINE_CLASS_STEAM;
		ai_special_flag:				AI_FLAG_PASSENGER;
		
		name:							string(STR_NAME_SUBURBAN_III); 
		introduction_date:				date(2028,1,1);
		vehicle_life: 					35;
		model_life:						0;
		length:							7;
		
		weight: 						90 ton;
		speed:							90 mph;
		power:							1500 hp;
		tractive_effort_coefficient: 	0x18 nfo;
		cost_factor: 					0x1C nfo;
		running_cost_factor: 			0x32 nfo;
		
	}
	graphics {
		default:						sw_SUBURBAN_III_randomflip;
		visual_effect_and_powered:		sw_SUBURBAN_III_randomflip_steam;
		sound_effect:					sw_sfx_JIN;
		purchase:						spriteset_SUBURBAN_III_purchase;
		create_effect:					vfx_STEAM;
	}
}

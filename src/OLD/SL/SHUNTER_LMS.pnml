//Sprite References & Switches
spriteset(spriteset_SHUNTER_LMS_purchase, "gfx/32bpp.png") { template_fai_purchase(0, 0)}
alternative_sprites(spriteset_SHUNTER_LMS_purchase, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "gfx/SL/SHUNTER_LMS.png") { template_fai_purchase(0, 0)}

spriteset(spriteset_SHUNTER_LMS, "gfx/32bpp.png") { template_fai_21(0, 16)}
alternative_sprites(spriteset_SHUNTER_LMS, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "gfx/SL/SHUNTER_LMS.png") { template_fai_21(0, 16)}
spriteset(spriteset_SHUNTER_LMS_flip, "gfx/32bpp.png") { template_fai_21_r(0, 16)}
alternative_sprites(spriteset_SHUNTER_LMS_flip, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "gfx/SL/SHUNTER_LMS.png") { template_fai_21_r(0, 16)}

switch(FEAT_TRAINS, PARENT, sw_SHUNTER_LMS_flipcheck, vehicle_is_reversed){
	1: spriteset_SHUNTER_LMS;
	spriteset_SHUNTER_LMS_flip ;
}

switch(FEAT_TRAINS, PARENT, sw_SHUNTER_LMS_flipcheck_inv, vehicle_is_reversed){
	1: spriteset_SHUNTER_LMS_flip;
	spriteset_SHUNTER_LMS ;
}

random_switch (FEAT_TRAINS, SELF, sw_SHUNTER_LMS_randomflip) {
	1: sw_SHUNTER_LMS_flipcheck;
	1: sw_SHUNTER_LMS_flipcheck_inv;
}

switch(FEAT_TRAINS, PARENT, sw_SHUNTER_LMS_flipcheck_steam, vehicle_is_reversed){
	1: visual_effect_and_powered(VISUAL_EFFECT_STEAM, 0, ENABLE_WAGON_POWER);
	visual_effect_and_powered(VISUAL_EFFECT_STEAM, -4, ENABLE_WAGON_POWER) ;
}

switch(FEAT_TRAINS, PARENT, sw_SHUNTER_LMS_flipcheck_steam_inv, vehicle_is_reversed){
	1: visual_effect_and_powered(VISUAL_EFFECT_STEAM, -4, ENABLE_WAGON_POWER);
	visual_effect_and_powered(VISUAL_EFFECT_STEAM, 0, ENABLE_WAGON_POWER) ;
}

random_switch (FEAT_TRAINS, SELF, sw_SHUNTER_LMS_randomflip_steam) {
	dependent:	sw_SHUNTER_LMS_randomflip;
	1: sw_SHUNTER_LMS_flipcheck_steam_inv;
	1: sw_SHUNTER_LMS_flipcheck_steam;
}

//Vehicle
item (FEAT_TRAINS, ITEM_FAI_SL_SHUNTER_LMS) {
	property {
		sprite_id:						SPRITE_ID_NEW_TRAIN;
		refit_cost:						0;
		climates_available:				ALL_CLIMATES;
		bitmask_vehicle_info:			0;
		air_drag_coefficient:			0x24 nfo;
		misc_flags:						bitmask(TRAIN_FLAG_SPRITE_STACK);
		extra_flags:				    bitmask(VEHICLE_FLAG_DISABLE_NEW_VEHICLE_MESSAGE, VEHICLE_FLAG_DISABLE_EXCLUSIVE_PREVIEW, VEHICLE_FLAG_SYNC_VARIANT_EXCLUSIVE_PREVIEW, VEHICLE_FLAG_SYNC_VARIANT_RELIABILITY);
		
		track_type:						RAIL;
		running_cost_base:				RUNNING_COST_STEAM;
		engine_class: 					ENGINE_CLASS_STEAM;
		ai_special_flag:				AI_FLAG_CARGO;
		
		name:							string(STR_NAME_SHUNTER_LMS); 
		introduction_date:				date(1910,1,1);
		vehicle_life: 					40;
		model_life:						52;
		length:							5;
		
		weight: 						48 ton;
		speed:							35 mph;
		power:							500 hp;
		tractive_effort_coefficient: 	0x23 nfo;
		cost_factor: 					0x0B nfo;
		running_cost_factor: 			0x0E nfo;
		
		variant_group: 			        ITEM_FAI_SL_SHUNTER;
		
	}
	graphics {
		default:						sw_SHUNTER_LMS_randomflip;
		visual_effect_and_powered:		sw_SHUNTER_LMS_randomflip_steam;
		sound_effect:					sw_sfx_JIN;
		purchase:						spriteset_SHUNTER_LMS_purchase;
		create_effect:					vfx_STEAM;
	}
}

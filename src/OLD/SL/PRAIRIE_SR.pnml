//Sprite References & Switches
spriteset(spriteset_PRAIRIE_SR_purchase, "gfx/32bpp.png") { template_fai_purchase(0, 0)}
alternative_sprites(spriteset_PRAIRIE_SR_purchase, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "gfx/SL/PRAIRIE_SR.png") { template_fai_purchase(0, 0)}

spriteset(spriteset_PRAIRIE_SR, "gfx/32bpp.png") { template_fai_25(0, 16)}
alternative_sprites(spriteset_PRAIRIE_SR, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "gfx/SL/PRAIRIE_SR.png") { template_fai_25(0, 16)}
spriteset(spriteset_PRAIRIE_SR_flip, "gfx/32bpp.png") { template_fai_25_r(0, 16)}
alternative_sprites(spriteset_PRAIRIE_SR_flip, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "gfx/SL/PRAIRIE_SR.png") { template_fai_25_r(0, 16)}

switch(FEAT_TRAINS, PARENT, sw_PRAIRIE_SR_flipcheck, vehicle_is_reversed){
	1: spriteset_PRAIRIE_SR;
	spriteset_PRAIRIE_SR_flip ;
}

switch(FEAT_TRAINS, PARENT, sw_PRAIRIE_SR_flipcheck_inv, vehicle_is_reversed){
	1: spriteset_PRAIRIE_SR_flip;
	spriteset_PRAIRIE_SR ;
}

random_switch (FEAT_TRAINS, SELF, sw_PRAIRIE_SR_randomflip) {
	1: sw_PRAIRIE_SR_flipcheck;
	1: sw_PRAIRIE_SR_flipcheck_inv;
}

switch(FEAT_TRAINS, PARENT, sw_PRAIRIE_SR_flipcheck_steam, vehicle_is_reversed){
	1: visual_effect_and_powered(VISUAL_EFFECT_STEAM, 0, ENABLE_WAGON_POWER);
	visual_effect_and_powered(VISUAL_EFFECT_STEAM, -4, ENABLE_WAGON_POWER) ;
}

switch(FEAT_TRAINS, PARENT, sw_PRAIRIE_SR_flipcheck_steam_inv, vehicle_is_reversed){
	1: visual_effect_and_powered(VISUAL_EFFECT_STEAM, -4, ENABLE_WAGON_POWER);
	visual_effect_and_powered(VISUAL_EFFECT_STEAM, 0, ENABLE_WAGON_POWER) ;
}

random_switch (FEAT_TRAINS, SELF, sw_PRAIRIE_SR_randomflip_steam) {
	dependent:	sw_PRAIRIE_SR_randomflip;
	1: sw_PRAIRIE_SR_flipcheck_steam_inv;
	1: sw_PRAIRIE_SR_flipcheck_steam;
}

//Vehicle
item (FEAT_TRAINS, ITEM_FAI_SL_PRAIRIE_SR) {
	property {
		sprite_id:						SPRITE_ID_NEW_TRAIN;
		refit_cost:						0;
		climates_available:				ALL_CLIMATES;
		bitmask_vehicle_info:			0;
		air_drag_coefficient:			0x19 nfo;
		misc_flags:						bitmask(TRAIN_FLAG_SPRITE_STACK);
		extra_flags:				    bitmask(VEHICLE_FLAG_DISABLE_NEW_VEHICLE_MESSAGE, VEHICLE_FLAG_DISABLE_EXCLUSIVE_PREVIEW, VEHICLE_FLAG_SYNC_VARIANT_EXCLUSIVE_PREVIEW, VEHICLE_FLAG_SYNC_VARIANT_RELIABILITY);
		
		track_type:						RAIL;
		running_cost_base:				RUNNING_COST_STEAM;
		engine_class: 					ENGINE_CLASS_STEAM;
		ai_special_flag:				AI_FLAG_PASSENGER;
		
		name:							string(STR_NAME_PRAIRIE_SR); 
		introduction_date:				date(1903,1,1);
		vehicle_life: 					36;
		model_life:						62;
		length:							6;
		
		weight: 						57 ton;
		speed:							50 mph;
		power:							650 hp;
		tractive_effort_coefficient: 	0x2B nfo;
		cost_factor: 					0x11 nfo;
		running_cost_factor: 			0x16 nfo;
		
		variant_group: 			        ITEM_FAI_SL_PRAIRIE;
		
	}
	graphics {
		default:						sw_PRAIRIE_SR_randomflip;
		visual_effect_and_powered:		sw_PRAIRIE_SR_randomflip_steam;
		sound_effect:					sw_sfx_JIN;
		purchase:						spriteset_PRAIRIE_SR_purchase;
		create_effect:					vfx_STEAM;
	}
}
